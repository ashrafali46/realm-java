// Find property in either System environment or Gradle properties.
// If set in both places, Gradle properties win.
def getPropertyValue(String propertyName) {
    def value = System.getenv(propertyName)
    if (project.hasProperty(propertyName)) {
        value = project.getProperty(propertyName)
    }
    return value
}

// Default values
ext["signing.keyId"] = 'BD9104E9'
ext["sonatypeStagingProfileId"] = '78c19333e4450f'

// Set properties either from a properties file or environment parameters
File secretPropsFile = new File(buildscript.sourceFile.getParent(), 'mavencentral.properties')
if (secretPropsFile.exists()) {
    Properties p = new Properties()
    p.load(new FileInputStream(secretPropsFile))
    p.each { name, value ->
        ext[name] = value
    }
} else {
    ext["signBuild"] = getPropertyValue('signBuild')
    ext["signing.password"] = getPropertyValue('signPassword')
    ext["signing.secretKeyRingFile"] = getPropertyValue('signSecretRingFile')
    ext["ossrhUsername"] = getPropertyValue('ossrhUsername')
    ext["ossrhPassword"] = getPropertyValue('ossrhPassword')
}
